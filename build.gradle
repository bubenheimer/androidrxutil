/*
 * Copyright (c) 2015-2020 Uli Bubenheimer
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

buildscript {
    ext.android_plugin_version = '4.1.0-rc03'
    ext.kotlin_version = '1.4.10'

    repositories {
        google()
        gradlePluginPortal()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$android_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    mavenLocal()
    google()
    jcenter()
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-android'

group = 'org.bubenheimer'
version = '1.0-SNAPSHOT'

android {
    compileSdkVersion 30

    defaultConfig {
        targetSdkVersion 30
        minSdkVersion 14
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
        debug {
        }
        release {
            shrinkResources false
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = [
                "-progressive",
                "-Xexplicit-api=strict",
                "-Xopt-in=kotlin.contracts.ExperimentalContracts"
        ]
    }

    buildFeatures {
        buildConfig false
    }

    dependencies {
        String archCompLifecycleVersion = '2.2.0'

        debugImplementation 'org.bubenheimer:androidutil-debug:1.0-SNAPSHOT'
        releaseImplementation 'org.bubenheimer:androidutil-release:1.0-SNAPSHOT'

        implementation 'org.bubenheimer:javautil:1.0-SNAPSHOT'

        implementation 'org.bubenheimer:kotlinutil:1.0-SNAPSHOT'

        implementation 'androidx.core:core:1.3.1'

        implementation "androidx.lifecycle:lifecycle-livedata-ktx:$archCompLifecycleVersion"
        implementation "androidx.lifecycle:lifecycle-common-java8:$archCompLifecycleVersion"
        implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$archCompLifecycleVersion"

        //Use api to make Scheduler super class available to users
        api 'io.reactivex.rxjava3:rxjava:3.0.6'
        implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    }

    task debugSourceJar(type: Jar) {
        sourceSets {
            from debug.java.srcDirs, main.java.srcDirs
        }
        //noinspection GroovyAccessibility
        archiveClassifier = "sources"
    }
}

publishing {
    publications {
        debug(MavenPublication) {
            afterEvaluate {
                from components.debug
                artifactId = "$project.name-debug"

                artifact(debugSourceJar)
            }
        }

        release(MavenPublication) {
            afterEvaluate {
                from components.release
                artifactId = "$project.name-release"
            }
        }
    }
}
