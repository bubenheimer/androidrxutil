/*
 * Copyright (c) 2015-2016 Uli Bubenheimer.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import net.ltgt.gradle.errorprone.CheckSeverity

buildscript {
    ext.android_plugin_version = '3.4.0-alpha10'
    ext.kotlin_version = '1.3.11'

    repositories {
        google()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0-alpha10'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.6.1'
    }
}

repositories {
    mavenLocal()
    google()
    jcenter()
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'kotlin-android'
apply plugin: 'net.ltgt.errorprone'

group = 'org.bubenheimer'
version = '1.0-SNAPSHOT'

android {
    compileSdkVersion 28

    defaultConfig {
        targetSdkVersion 28
        minSdkVersion 14
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
        debug {
        }
        release {
            shrinkResources false
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // TODO change based on resolution of https://issuetracker.google.com/issues/72050365
    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }
}

dependencies {
    String archCompLifecycleVersion = '2.0.0'

    errorprone 'com.google.errorprone:error_prone_core:2.3.2'
    //noinspection GradleDynamicVersion
    errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'

    implementation 'org.bubenheimer:androidutil:1.0-SNAPSHOT'
    implementation 'org.bubenheimer:javautil:1.0-SNAPSHOT'

    implementation 'androidx.core:core:1.0.1'

    implementation "androidx.lifecycle:lifecycle-common-java8:$archCompLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$archCompLifecycleVersion"

    //Use api to make Scheduler super class available to users
    api 'io.reactivex.rxjava2:rxjava:2.2.5'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.google.guava:guava:27.0.1-android'

    testImplementation 'junit:junit:4.12'
}
